
DROP TABLE IF EXISTS #FATO
SELECT * 
INTO #FATO
FROM OPENQUERY([10.251.1.36],'
SELECT 
     NOME_PES			AS NOME_OPERADOR
,    USUAR_PES			AS CPF_OPERADOR
,    STATI_PES			AS STATUS_OPERADOR
,    ALCAD_PES			AS NIVEL_ALCADA
,    UPPER(DESCR_CGO)	AS CARGO_CRM
,    NOME_GUS			AS GRUPO_CRM
,    IDGUS_PES
FROM	  NECTAR.DBO.TB_PESSOAL         WITH( NOLOCK )
LEFT JOIN NECTAR.DBO.TB_CARGO           WITH( NOLOCK ) ON IDCGO_CGO = IDCGO_PES
LEFT JOIN NECTAR.DBO.TB_GRUPOUSUARIO    WITH( NOLOCK ) ON IDGUS_GUS = IDGUS_PES')



--> Tabela Dimensâo
DROP TABLE IF EXISTS #DIMENSAO_VISUALIZACAO
SELECT *
INTO #DIMENSAO_VISUALIZACAO
FROM OPENQUERY([10.251.1.36],'
SELECT DISTINCT
	IDGUS_MEN
,	RW		=	ROW_NUMBER() OVER ( PARTITION BY IDGUS_MEN ORDER BY IDGUS_MEN DESC)
,	DESCR_MEN 
FROM NECTAR.DBO.TB_MENU WITH ( NOLOCK )')


DECLARE @ACESSOS NVARCHAR(MAX) = 
									STUFF((
										 SELECT DISTINCT ',' + QUOTENAME(DESCR_MEN) 
										 FROM #DIMENSAO_VISUALIZACAO 
										 FOR XML PATH('')
										 ),1,1,'')
DECLARE @TSQL NVARCHAR(MAX) = '
DROP TABLE IF EXISTS ##PIVOT
SELECT 
	IDGUS_MEN	
,	'+@ACESSOS+' 
INTO ##PIVOT
FROM #DIMENSAO_VISUALIZACAO 
PIVOT(
		MAX (
			 RW
		    )
			FOR DESCR_MEN IN (
								'+@ACESSOS+'
							  )
		) PVT '
EXEC(
	 @TSQL
	)

DROP TABLE IF EXISTS #FINAL
SELECT *
INTO #FINAL
FROM #FATO 
CROSS APPLY (
			 SELECT *
			 FROM ##PIVOT 
			 WHERE ##PIVOT.IDGUS_MEN = #FATO.IDGUS_PES
			) As Dimensao

ALTER TABLE #FINAL DROP COLUMN IDGUS_MEN

SELECT *
FROM #FINAL